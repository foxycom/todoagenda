buildscript {
    repositories {
        google()
        jcenter()
    }

    ext {
        compileSdkVersion = 29 as int
        buildToolsVersion = '29.0.2'
        minSdkVersion = 24 as int
        targetSdkVersion = 29 as int

        // Lookup the latest here: https://mvnrepository.com/
        annotationVersion = '1.0.1'
        appCompatVersion = '1.2.0'        // https://mvnrepository.com/artifact/androidx.appcompat/appcompat
        espressoCoreVersion = '3.3.0'
        jodaTimeVersion = '2.10.3'
        junitVersion = '4.13'
        junitJupiterVersion = '5.7.0'
        preferenceVersion = '1.1.1'
        testRulesVersion = '1.3.0'
        testRunnerVersion = '1.3.0'
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
    }

    // https://github.com/facebook/flipper/issues/146#issuecomment-463667556
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == "androidx") {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion "${targetSdkVersion}.+"
                }
            }
        }
    }
}
allprojects {
    repositories {
        google()
        jcenter()
    }
}

ext {
    // We need to use the -P format for properties, so we need to access them using the "project" global variable
    // So we do not use the 'System.properties.getProperty("carvedTests") ?: "false"' pattern here
    if ( ! project.hasProperty("jacocoEnabled")) {
        // Log a warning here
        println("jacocoEnabled property not defined. Default it to false")
        jacocoEnabled = "false"
    }
    if (! project.hasProperty("carvedTests")) {
        println("carvedTests property not defined. Default it to false")
        carvedTests = "false"
    }

    println "Summary  of props:"
    println "jacocoEnabled: " + jacocoEnabled.toBoolean()
    println "carvedTests:   " + carvedTests.toBoolean()

    // Set default values here. Those may be overwritten inside app/build.gradle
    testReportPrefix = "${buildDir}/carvedTest/testExecution"
    coverageReportPrefix = "${buildDir}/carvedTest/coverage"

}